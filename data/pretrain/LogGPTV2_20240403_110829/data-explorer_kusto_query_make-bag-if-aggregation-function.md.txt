---
title:  make_bag_if() (aggregation function)
description: Learn how to use the make_bag_if() function to create a dynamic JSON property bag of expression values where the predicate evaluates to true.
ms.reviewer: alexans
ms.topic: reference
ms.date: 11/13/2023
---
# make_bag_if() (aggregation function)
Creates a `dynamic` JSON property bag (dictionary) of *expr* values in records for which *predicate* evaluates to `true`.
[!INCLUDE [ignore-nulls](../../includes/ignore-nulls.md)]
[!INCLUDE [data-explorer-agg-function-summarize-note](../../includes/data-explorer-agg-function-summarize-note.md)]
## Syntax
`make_bag_if(`*expr*`,` *predicate* [`,` *maxSize*]`)`
[!INCLUDE [syntax-conventions-note](../../includes/syntax-conventions-note.md)]
## Parameters
| Name | Type | Required | Description |
|--|--|--|--|
| *expr* | `dynamic` |  :heavy_check_mark: | The expression used for the aggregation calculation. |
| *predicate* | `bool` |  :heavy_check_mark: | The predicate that evaluates to `true`, in order for *expr* to be added to the result. |
| *maxSize* | `int` |  | The limit on the maximum number of elements returned. The default and max value is 1048576. |
## Returns
Returns a `dynamic` JSON property bag (dictionary) of *expr* values in records for which *predicate* evaluates to `true`. Non-dictionary values will be skipped.
If a key appears in more than one row, an arbitrary value, out of the possible values for this key, will be selected.
> [!NOTE]
> This function without the predicate is similar to [`make_bag`](./make-bag-aggregation-function.md).
## Example
The following example shows a packed JSON property bag.
> [!div class="nextstepaction"]
> Run the query
```kusto
let T = datatable(prop:string, value:string, predicate:bool)
[
    "prop01", "val_a", true,
    "prop02", "val_b", false,
    "prop03", "val_c", true
];
T
| extend p = bag_pack(prop, value)
| summarize dict=make_bag_if(p, predicate)
```
**Output**
|dict|
|----|
|{ "prop01": "val_a", "prop03": "val_c" } |
Use [bag_unpack()](bag-unpack-plugin.md) plugin for transforming the bag keys in the make_bag_if() output into columns.
> [!div class="nextstepaction"]
> Run the query
```kusto
let T = datatable(prop:string, value:string, predicate:bool)
[
    "prop01", "val_a", true,
    "prop02", "val_b", false,
    "prop03", "val_c", true
];
T
| extend p = bag_pack(prop, value)
| summarize bag=make_bag_if(p, predicate)
| evaluate bag_unpack(bag)
```
**Output**
|prop01|prop03|
|---|---|
|val_a|val_c|